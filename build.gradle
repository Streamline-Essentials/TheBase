plugins {
    id 'java'
    id 'io.freefair.lombok' version '8.10.2'
    id 'com.gradleup.shadow' version '8.3.6'
    id 'maven-publish'
}

group = project.properties['group'] ?: 'gg.drak'
version = project.properties['version'] ?: '1.0.0'

java.sourceCompatibility = JavaVersion.VERSION_11
java.targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
    mavenLocal()
    maven { url 'https://jitpack.io' }
}

configurations {
    implementation {
        exclude group: 'org.slf4j', module: 'slf4j-api'
        exclude group: 'org.apache.logging.log4j'
    }
    shadow {
        exclude group: 'org.slf4j', module: 'slf4j-api'
        exclude group: 'org.apache.logging.log4j'
    }
}

dependencies {
    // Databases
    implementation 'mysql:mysql-connector-java:8.0.33'
    implementation 'org.xerial:sqlite-jdbc:3.46.1.0'
    implementation 'com.zaxxer:HikariCP:5.1.0'
    // Exp4j
    implementation 'net.objecthunter:exp4j:0.4.8'
    // RE2J
    implementation 'com.google.re2j:re2j:1.7'
    // JetBrains Annotations
    implementation 'org.jetbrains:annotations:24.0.1'
    // SimplixStorage
    implementation 'com.github.DragonFixes:SimplixStorage:master-SNAPSHOT'
}

shadowJar {
    Map<String, String> relocations = [
            'de.leonhard': 'gg.drak.thebase.lib.leonhard',
            'com.mysql': 'gg.drak.thebase.lib.mysql',
            'com.zaxxer.hikari': 'gg.drak.thebase.lib.hikari',
            'net.objecthunter.exp4j': 'gg.drak.thebase.lib.exp4j',
            'com.google.re2j': 'gg.drak.thebase.lib.re2j',
            'com.google.gson': 'gg.drak.thebase.lib.google.gson',
            'com.google.guava': 'gg.drak.thebase.lib.google.guava',
            'org.sqlite': 'gg.drak.thebase.lib.sqlite',
            'org.xerial': 'gg.drak.thebase.lib.xerial',
            'org.apache.commons': 'gg.drak.thebase.lib.apache.commons',
            'org.yaml': 'gg.drak.thebase.lib.yaml',
            'org.bstats': 'gg.drak.thebase.lib.bstats'
    ]
    relocations.each { src, dest -> relocate src, dest }

    exclude 'com/google/common/**'
    exclude 'org/apache/logging/log4j/**'
    exclude 'org/slf4j/**'
    exclude 'META-INF/**'
    exclude '**/LICENSE'
    exclude '**/README.md'
    exclude '**/docs/**'

    archiveFileName = "${project.name}-${project.version}.jar"
}

java {
    withSourcesJar()
    withJavadocJar()
}

wrapper {
    gradleVersion = '8.9'
    distributionType = Wrapper.DistributionType.BIN
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId project.group
            artifactId "$project.name"
            version project.version

            from components.java
        }
    }

    repositories {
        maven {
            url = "https://repo.codemc.io/repository/streamline-essentials/"

            def mavenUsername = System.getenv("JENKINS_NEXUS_USERNAME")
            def mavenPassword = System.getenv("JENKINS_NEXUS_PASSWORD")

            if (mavenUsername != null && mavenPassword != null) {
                credentials {
                    username = mavenUsername
                    password = mavenPassword
                }
            }
        }
    }
}